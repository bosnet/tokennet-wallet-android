apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.boscoin.toknenet.wallet"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "BOSTokennetWallet-v${versionName}c${versionCode}")
    }


 


    buildTypes {
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"
        def TOKEN_GET = "TOKEN_GET"
        def NETWORK_PH = "NETWORK_PH"
        def AES_DEBUG = "AES_DEBUG"

        debug {
            applicationIdSuffix ".debug"
            buildConfigField BOOLEAN, TOKEN_GET, TRUE
            buildConfigField BOOLEAN, AES_DEBUG, TRUE
            buildConfigField STRING, NETWORK_PH, '"Test BOS Token Network ; tokennet.test; September 2017"'
        }

        release {

            debuggable false
            minifyEnabled true
            buildConfigField BOOLEAN, TOKEN_GET, FALSE
            buildConfigField BOOLEAN, AES_DEBUG, FALSE
            buildConfigField STRING, NETWORK_PH, '"BOS Token Network ; October 2017"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    task switchToDebug(type: Copy) {
        description = 'Switches to DEBUG google-services.json'
        from "src/debug"
        include "google-services.json"
        into "."
    }

    task switchToRelease(type: Copy) {
        description = 'Switches to RELEASE google-services.json'
        from "src/release"
        include "google-services.json"
        into "."
    }

    afterEvaluate {
        processDebugGoogleServices.dependsOn switchToDebug
        processReleaseGoogleServices.dependsOn switchToRelease
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.google.android.gms:play-services-plus:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

}


apply plugin: 'com.google.gms.google-services'
